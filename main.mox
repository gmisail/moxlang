class Vector

    var data : Pointer<int> = NULL
    var size : int = 0
    var count : int = 0

    function init()

    end

    function destroy()
        free(self.data)
    end

    function count() -> int
        return self.count
    end

    function push(value : int)
        if(self.size == 0)
            self.size = 4
            self.data = calloc(self.size, sizeof(int))

            for(i from 0 to self.size)
                self.data[i] = 0
            end
        end

        if(self.size == self.count)
            self.size = self.size * 2
            self.data = realloc(self.data, sizeof(int) * self.size)
        end

        self.data[self.count] = value
        self.count = self.count + 1
    end

    function set(index : int, value : int)
        if(index < self.count and index >= 0)
            self.data[index] = value
        end
    end

    function get(index : int) -> int
        if(index < self.count and index >= 0)
            return self.data[index]
        end

        return 0
    end

    function pop() -> int
        var val : int = self.get(self.count - 1)
        self.set(self.count - 1, 0)
        self.count = self.count - 1

        return val
    end

    function print()
        printf("[")
        for(i from 0 to self.count)
            if(i > 0)
                printf(", ")
            end

            printf("%i", self.get(i))
        end
        printf("]\n")
    end

end

function main() -> int
    var vector : Vector = new Vector()

    for(i from 0 to 15)
        vector.push(i)
        vector.print()
    end

    for(i from 0 to 15)
        vector.pop()
        vector.print()
    end

    delete vector
end