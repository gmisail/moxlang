extern NULL -> any

class Vector

    var data : Pointer<int> = NULL
    var len : int = 0
    var count : int = 0

    function init()

    end

    function destroy()
        free(self.data)
    end

    function count() -> int
        return self.count
    end

    function push(value : int)
        if(self.len == 0)
            self.len = 4
            self.data = calloc(self.len, size(int))

            for(i from 0 to self.len)
                self.data[i] = 0
            end
        end

        if(self.len == self.count)
            self.len = self.len * 2
            self.data = realloc(self.data, size(int) * self.len)
        end

        self.data[self.count] = value
        self.count = self.count + 1
    end

    function set(index : int, value : int)
        if(index < self.count and index >= 0)
            self.data[index] = value
        end
    end

    function get(index : int) -> int
        if(index < self.count and index >= 0)
            return self.data[index]
        end

        return 0
    end

    function pop() -> int
        var val : int = self.get(self.count - 1)

        self.set(self.count - 1, 0)
        self.count = self.count - 1

        return val
    end

    function print()
        printf("[")
        for(i from 0 to self.count)
            if(i > 0)
                printf(", ")
            end

            printf("%i", self.get(i))
        end
        printf("]\n")
    end

end
